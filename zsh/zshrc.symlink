#!/usr/bin/env zsh

# zmodload zsh/zprof

DOTFILES=$HOME/.dotfiles

typeset -U config_files
config_files=($DOTFILES/*/*.zsh)

# load in path files
for file in ${(M)config_files:#*/path.zsh}; do
	source "$file"
done

# statically load zsh plugins (via antibody)
ZSH_PLUGINS_FILE="$HOME/.zsh_plugins.sh"

if [ -s "$ZSH_PLUGINS_FILE" ]; then
	source "$ZSH_PLUGINS_FILE"
fi

# load non-antigen plugins

# pure
fpath+=$HOME/.zsh/pure

# evalcache
source $HOME/.zsh/evalcache/evalcache.plugin.zsh

# load everything but path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
	source "$file"
done

autoload -Uz compinit
for dump in ~/.zcompdump(N.mh+24); do
  compinit
done
compinit -C

# load completion files after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}; do
	source "$file"
done

# load functions
fpath=($DOTFILES/functions $fpath)
autoload -U "$DOTFILES"/functions/*(:t)

unset config_files

# load in local config
if [ -f "$HOME/.localrc" ]; then
	source "$HOME/.localrc"
fi

# zprof
